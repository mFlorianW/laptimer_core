cmake_minimum_required(VERSION 3.19)

project(LaptimerCore)
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/configuration/cmake/")

include(FeatureSummary)
include(CheckCCompilerFlag)
include(GNUInstallDirs)
include(PreCommit)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_POSITION_INDEPENDENT_CODE ON)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(SHARED_DATADIR ${CMAKE_INSTALL_DATAROOTDIR}/lappy)

option(ENABLE_TEST "Enable unit tests" OFF)
option(ENABLE_QT "This option enables QT implementations" ON)
option(ENABLE_PRECOMMIT "Enables automatic pre-commit install." OFF)
option(ENABLE_BOOST "Enables the boost support" OFF)
option(ENABLE_VCPKG "This option is enabled when building with VCPKG" OFF)
add_feature_info(PreCommit ENABLE_PRECOMMIT "Automatic install of the Python pre-commit tool.")

if(ENABLE_PRECOMMIT)
    setup_precommit()
endif()

if(ENABLE_QT)
    set(CMAKE_AUTOMOC ON)
    set(CMAKE_AUTORCC ON)
    set(CMAKE_AUTOUIC ON)

    find_package(Qt6 REQUIRED COMPONENTS Core Qml Gui Quick Widgets Network Location QuickWidgets)

    ADD_DEFINITIONS(-DQT_NO_KEYWORDS)

    if(QT_KNOWN_POLICY_QTP0001)
        qt_policy(SET QTP0001 NEW)
    endif()
endif()

find_package(KDBindings REQUIRED)
find_path(ARDUINOJSON_INCLUDE_DIRS "ArduinoJson.h")
include_directories(SYSTEM ${ARDUINOJSON_INCLUDE_DIRS})
find_package(SDL2 REQUIRED)
find_package(lvgl-ssd1322 REQUIRED)
find_package(lvgl-ssd1322-drivers REQUIRED)
if(ENABLE_VCPKG)
    find_package(unofficial-sqlite3 CONFIG REQUIRED)
    add_library(SQLite::SQLite3 ALIAS unofficial::sqlite3::sqlite3)
else()
    find_package(SQLite3 REQUIRED)
endif()
if(ENABLE_BOOST)
    find_package(Boost CONFIG REQUIRED)
    add_definitions(-DWITH_BOOST)
endif()

check_c_compiler_flag(-Wall HAS_WALL)
if(HAS_WALL)
    set(CMAKE_CXX_FLAGS "${CMAKE_C_FLAGS} -Wall")
endif()

add_subdirectory(libs)
add_subdirectory(db)
add_subdirectory(programs)

if(ENABLE_TEST)
    include(CTest)
    find_package(Catch2 CONFIG REQUIRED)
    if(ENABLE_QT)
        find_package(Qt6 REQUIRED Test)
    endif()
    enable_testing()
    add_subdirectory(tests)
endif(ENABLE_TEST)

feature_summary(WHAT ALL)
